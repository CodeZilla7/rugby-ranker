apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.ricknout.rugbyranker"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 2007
        versionName "2.0.7"
        setProperty('archivesBaseName', applicationId + '-v' + versionName)
        testInstrumentationRunner deps.androidX.test.instrumentationRunner
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    appName: '@string/app_name',
                    appIcon: '@mipmap/ic_launcher',
                    workManagerInitAuthorities: 'dev.ricknout.rugbyranker.workmanager-init'
            ]
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            manifestPlaceholders = [
                    appName: '@string/app_name_debug',
                    appIcon: '@mipmap/ic_launcher_debug',
                    workManagerInitAuthorities: 'dev.ricknout.rugbyranker.deug.workmanager-init'
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }
    buildFeatures {
        viewBinding = true
        compose = true
    }
    composeOptions {
        kotlinCompilerVersion versions.kotlin.kotlin
        kotlinCompilerExtensionVersion versions.androidX.compose
    }
}

dependencies {
    implementation project(':core')
    implementation project(':ranking')
    implementation project(':prediction')
    implementation project(':match')
    implementation project(':live')
    implementation project(':news')
    implementation project(':info')
    implementation project(':theme')
    implementation deps.androidX.drawerLayout
    implementation deps.androidX.viewPager2
    implementation deps.google.hilt.android
    implementation deps.androidX.compose.runtime.runtime
    kapt deps.androidX.room.compiler
    kapt deps.google.hilt.compiler
    kapt deps.androidX.hilt.compiler
    testImplementation deps.androidX.test.jUnit.ktx
    androidTestImplementation deps.androidX.test.core.ktx
    androidTestImplementation deps.androidX.test.runner
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
    }
}
